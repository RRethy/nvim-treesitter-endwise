((if_statement condition: (_) @cursor) @endable @indent (#endwise! "endif"))
(((for_loop (_) (_) @cursor) @indent) (#endwise! "endfor"))
(((while_loop condition: (_) @cursor) @indent) (#endwise! "endwhile"))
((function_definition . [((function_declaration name: (_) parameters: (_)) ["abort" "closure" "dict" "range"]* @cursor) ((function_declaration name: (_) parameters: (_)) @cursor)]) @endable @indent (#endwise! "endfunction"))
((try_statement) @cursor @endable @indent (#endwise! "endtry"))

((ERROR . ("if" @indent . (_) @cursor)) (#endwise! "endif"))
((ERROR . ("for" @indent . (_) . "in" . (_) @cursor)) (#endwise! "endfor"))
((ERROR . ("while" @indent . (_) @cursor)) (#endwise! "endwhile"))
((ERROR . ("function" @indent . [((function_declaration name: (_) parameters: (_)) ["abort" "closure" "dict" "range"]* @cursor) ((function_declaration name: (_) parameters: (_)) @cursor)])) (#endwise! "endfunction"))
((ERROR . ("try" @indent @cursor)) (#endwise! "endtry"))
