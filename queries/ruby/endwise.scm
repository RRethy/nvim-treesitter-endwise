((module name: (_) @cursor) @endable @indent (#endwise! "end"))
((class name: (_) @cursor superclass: (_)? @cursor) @endable @indent (#endwise! "end"))
((singleton_class "class" . "<<" . value: (_) @cursor) @endable @indent (#endwise! "end"))
((method name: (_) @cursor parameters: (_)? @cursor) @endable @indent (#endwise! "end"))
((singleton_method name: (_) @cursor parameters: (_)? @cursor) @endable @indent (#endwise! "end"))
((while condition: (_) @cursor body: (do ("do")? @cursor) @endable) @indent (#endwise! "end"))
((until condition: (_) @cursor body: (do ("do")? @cursor) @endable) @indent (#endwise! "end"))
((for value: (_) @cursor body: (do ("do")? @cursor) @endable) @indent (#endwise! "end"))
((do_block "do" @cursor parameters: (_)? @cursor) @endable @indent (#endwise! "end"))
((if condition: (_) @cursor) @endable @indent (#endwise! "end"))
((begin "begin" @cursor . (rescue "rescue" @cursor exceptions: (_)? @cursor)? . (ensure "ensure" @cursor)?) @endable @indent (#endwise! "end"))
((unless condition: (_) @cursor) @endable @indent (#endwise! "end"))
((case value: (_) @cursor) @endable @indent (#endwise! "end" nil "end" 0))
((case) @cursor @endable @indent (#endwise! "end" nil "end" 0))
((heredoc_beginning) @cursor @endable @indent (#endwise! "" @cursor "heredoc_end" 0 "<<\\~\\?\\zs.*"))

((ERROR ("module" @indent . [(constant) (scope_resolution)] @cursor)) (#endwise! "end"))
((ERROR ("class" @indent . [(constant) (scope_resolution)] @cursor . (superclass)? @cursor)) (#endwise! "end"))
((ERROR ("class" @indent . "<<" . (_) @cursor)) (#endwise! "end"))
((ERROR ("def" @indent . (identifier) @cursor . (method_parameters)? @cursor)) (#endwise! "end"))
((ERROR ("def" @indent . (identifier) . "." . (identifier) @cursor . (method_parameters)? @cursor)) (#endwise! "end"))
((ERROR ("while" @indent . (_) @cursor . "do"? @cursor)) (#endwise! "end"))
((ERROR ("until" @indent . (_) @cursor . "do"? @cursor)) (#endwise! "end"))
((ERROR ("for" @indent . (_) . (in . "in" . (_) @cursor) . "do"? @cursor)) (#endwise! "end"))
((ERROR ("do" @cursor @indent . (block_parameters)? @cursor)) (#endwise! "end"))
((ERROR ("begin" @cursor @indent . ["rescue" @cursor (rescue "rescue" @cursor exceptions: (_)? @cursor)]? . ["ensure" (ensure "ensure" @cursor)]?)) (#endwise! "end"))
((ERROR ("if" @indent . (_) @cursor . (then)? @cursor)) (#endwise! "end"))
((ERROR ("unless" @indent . (_) @cursor . (then)? @cursor)) (#endwise! "end"))
